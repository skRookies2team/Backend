plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.0'
    id 'io.spring.dependency-management' version '1.1.4'
}

group = 'com.story'
version = '1.0-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'  // WebClient for AI server calls
    implementation 'org.springframework.boot:spring-boot-starter-security'  // Spring Security

    // Database
    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'

    // Flyway for database migration
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-mysql'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.3'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.3'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // JSON processing
    implementation 'com.fasterxml.jackson.core:jackson-databind'

    // Swagger/OpenAPI
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.3.0'

    // AWS SDK for S3
    implementation 'com.amazonaws:aws-java-sdk-s3:1.12.565'

    // .env file support
    implementation 'me.paulschwarz:spring-dotenv:4.0.0'

    // --- [FIX] Java 17에서 ScriptEngine("JavaScript") 사용을 위한 Nashorn 엔진 추가 ---
    implementation 'org.openjdk.nashorn:nashorn-core:15.4'
    // -------------------------------------------------------------------------

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

// .env 파일 읽기 함수 (재사용)
def loadEnvFile() {
    def envFile = file('.env')
    if (envFile.exists()) {
        def envMap = [:]
        envFile.readLines().each { line ->
            if (line && !line.startsWith('#') && line.contains('=')) {
                def (key, value) = line.split('=', 2)
                envMap[key.trim()] = value.trim()
            }
        }
        return envMap
    }
    return [:]
}

bootRun {
    // JVM 메모리 설정 (메모리 부족 에러 방지)
    jvmArgs = [
            '-Xms256m',  // 초기 힙 메모리
            '-Xmx512m'   // 최대 힙 메모리
    ]
}

test {
    useJUnitPlatform()

    // 시스템 프로퍼티를 테스트에 전달
    systemProperty 'spring.profiles.active', System.getProperty('spring.profiles.active', 'test')

    // 환경 변수 사용 가능
    if (System.getenv('SPRING_PROFILES_ACTIVE')) {
        systemProperty 'spring.profiles.active', System.getenv('SPRING_PROFILES_ACTIVE')
    }
}